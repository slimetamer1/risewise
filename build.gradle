buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0'
        // classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.11"
    }
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
// apply plugin: 'jacoco-android'

android {
    compileSdkVersion 26
    flavorDimensions "default"
    defaultConfig {
        applicationId "com.better.alarm"
        minSdkVersion 16
        targetSdkVersion 26

        testApplicationId "com.better.alarm.test"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // multiDexEnabled true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "default"
    productFlavors {
        develop {
            applicationId "com.better.alarm"
        }
        premium {
            applicationId "com.premium.alarm"
        }
    }

    lintOptions {
        abortOnError false
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 20 * 60 * 1000  // 20 minutes
        installOptions "-d", "-t"
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
repositories {
    jcenter()
    google()
}

dependencies {
    // App dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.11"
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'ch.acra:acra:4.6.1'
    implementation 'com.melnykov:floatingactionbutton:1.2.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    // Testing-only dependencies
    testImplementation 'net.wuerl.kotlin:assertj-core-kotlin:0.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.bartoszlipinski:cortado:1.2.0'
    //for some tests which do not work with espresso on travis
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.2.1'
}
